import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

ext {
    configFileText = new File("${projectDir}/otc-config.android.json").getText('UTF-8')
    config = JsonOutput.toJson(new JsonSlurper().parseText(configFileText)).replaceAll("\"", "\\\\\"")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "CONFIGURATION", "\"${config}\""
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compileOnly 'com.android.support:support-annotations:28.0.0'

    api project(':Core')
    api project(':PayPalDataCollector')

    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.skyscreamer:jsonassert:1.4.0'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testImplementation project(':TestUtils')
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.paypalcv1cv1"
}

group = 'com.paypalcv1cv1.android.sdk'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'paypalcv1-one-touch'

            pom.project {
                name 'paypalcv1-one-touch'
                groupId = 'com.paypalcv1cv1.android.sdk'
                version = "${android.defaultConfig.versionName}"
                description 'PayPal SDK for handling app and browser switch based payments.'
            }
        }
    }
}
